plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'org.gradlex.extra-java-module-info' version '1.0'
}

version '2.0.0'

dependencies {

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    implementation project(path: ':fopcore', configuration: 'moduleJar')
    implementation group: 'com.github.cliftonlabs', name: 'json-simple', version: jsonSimpleVersion
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    implementation group: 'org.slf4j', name: 'slf4j-jdk-platform-logging', version: slf4jVersion
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: sqliteVersion
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: jakartaXmlBindVersion
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: javaxXmlBindVersion
    implementation group: 'jakarta.xml.ws', name: 'jakarta.xml.ws-api', version: jakartaXmlVersion
    implementation group: 'com.sun.xml.ws', name: 'jaxws-rt', version: sunXmlWsVersion
}

application {
    mainModule.set('emk')
    mainClass.set('com.github.bademux.emk.Application')
}

ext {
    exports = [
            'jdk.crypto.cryptoki/sun.security.pkcs11.wrapper',
            'java.xml.crypto/com.sun.org.apache.xml.internal.security',
            'java.xml.crypto/com.sun.org.apache.xml.internal.security.signature',
            'java.xml.crypto/com.sun.org.apache.xml.internal.security.utils',
            'java.xml.crypto/com.sun.org.apache.xml.internal.security.utils.resolver',
            'java.xml.crypto/org.jcp.xml.dsig.internal.dom',
            'java.base/sun.security.rsa',
            'java.base/sun.security.x509',
    ]
            .collect({ ['--add-exports', "$it=${application.mainModule.get()}"] })
            .flatten()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += exports
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = openfxVersion
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing']
}

run {
    enableAssertions = true
    jvmArgs = exports + '-Dfile.encoding=UTF-8'
}

test {
    useJUnitPlatform()
}

jlink {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'emk'
        noConsole = true
        jvmArgs = exports + ['-Xmx1G', '-Dfile.encoding=UTF-8', '-Dorg.slf4j.simpleLogger.logFile=emk.log', '-Dorg.slf4j.simpleLogger.defaultLogLevel=info']
    }
}

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(true)
    module('com.github.cliftonlabs:json-simple', 'com.googlecode.jsonsimple') {
        exports 'org.json.simple'
        exports 'org.json.simple.parser'
    }

}
